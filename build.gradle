buildscript {
	ext {
		springBootVersion = '3.2.6'
		lombokVersion = '1.18.32'
		mapStructVersion = '1.3.1.Final'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version '1.1.5'
}

apply from: "gradle/jacoco.gradle"

group = 'com.avmathman'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	integrationTestRuntime.extendsFrom testRuntimeOnly
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	integrationTestImplementation.extendsFrom testImplementation
}

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			srcDir file('src/integration-test/java')

			compileClasspath += main.output + test.output + main.compileClasspath + test.compileClasspath
			runtimeClasspath += main.output + test.output + main.runtimeClasspath + test.runtimeClasspath
		}

		resources.srcDir file('src/integration-test/resources')
	}
}

dependencies {

	// Spring dependencies:
	implementation "org.springframework.boot:spring-boot-starter-data-mongodb:${springBootVersion}"
//	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

	// Annotation processors:
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	implementation("org.mapstruct:mapstruct:$mapStructVersion")
	compileOnly("org.mapstruct:mapstruct-processor:$mapStructVersion")
	annotationProcessor (
			"org.projectlombok:lombok:${lombokVersion}",
			"org.mapstruct:mapstruct-processor:$mapStructVersion"
	)

	// Test dependencies
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	testRuntimeOnly "com.h2database:h2"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

tasks.named('test') {
	useJUnitPlatform()

	finalizedBy jacocoTestReport
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform()

	description = "Runs the integration tests"
	group = "verification"
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	maxHeapSize = '1024m'
}
